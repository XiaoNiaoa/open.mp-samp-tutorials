# 新的代码优化

优化技巧总数：13 | 更新时间：2016 年 6 月 21 日 | 作者: Yashas | 编辑: 小鸟

相关主题:
- [技巧与窍门 by Slice](https://sampforum.blast.hk/showthread.php?tid=216730)
- [为什么不能将字符串设置为 256 个单元？ by ******](https://sampforum.blast.hk/showthread.php?tid=580165)
- [代码优化 by ******](https://sampforum.blast.hk/showthread.php?pid=3517039#pid3517039)

目前该主题有以下技巧:
- [数组比普通变量慢](#数组比普通变量慢)
- [事先知道函数名时不要使用CallLocalFunction和funcidx](#事先知道函数名时不要使用calllocalfunction和funcidx)
- [本地代码比 PAWN 代码快得多](#本地代码比pawn代码快得多)
- [循环中的条件](#循环中的条件)
- [为多个变量赋值](#为多个变量赋值)
- [延迟声明局部变量](#延迟声明局部变量)
- [简化和重写数学运算以避免昂贵的操作](#简化和重写数学运算以避免昂贵的操作)
- [memcpy和strfind等操作也适用于数组](#memcpy和strfind等操作也适用于数组)
- [使用CallRemoteFunction真的值得吗](#使用callremotefunction真的值得吗)
- [多次访问数组元素](#多次访问数组元素)
- [不要在表达式中混合使用浮点数和整数（by Mauzen)](#不要在表达式中混合使用浮点数和整数)
- [不必要地使用Streamer](#不必要地使用streamer)
- [函数使用的好坏（优化二维数组操作代码）](#函数使用的好坏-优化二维数组操作代码)

其中有些优化会带来重大改进，有些则不会。你可以忽略一些次要的优化，优先编写可读性高的代码.

## 数组比普通变量慢

以下代码效率很低:
```pawn
new Float:pos[3];
GetPlayerPos(playerid, pos[0], pos[1], pos[2]);
```
以下是上述代码的汇编版本:
```pawn
zero.pri
addr.alt fffffff4
fill c ; 这 3 条指令负责将所有数组元素清零
break	; 38

addr.pri fffffff4 ;获取数组的地址
add.c 8 ;添加索引（索引 2 表示超前 2*4 字节）
load.i ;这将获取存储在该地址的值
push.pri ;推参数

addr.pri fffffff4 ;同上
add.c 4
load.i
push.pri
addr.pri fffffff4 ;同上
load.i
push.pri
```
下面是更高效地编写的等效代码:
```pawn
new Float:x, Float:y, Float:z;
GetPlayerPos(playerid, x, y , z);
```
以下是上述代码的汇编版本:
```pawn
push.c 0 //为堆栈中的变量腾出空间
push.c 0
push.c 0
push.adr fffffff4 //推参数
push.adr fffffff8
push.adr fffffffc
```
要访问数组元素时，编译器会使用以下算法:

第一个元素的地址 + 4*Index = Array[Index] 的存储位置（此公式仅适用于一维数组）

计算出数组元素的地址后，就可以检索该元素中存储的数据了.

这并不意味着一定不能使用数组。相反，您必须明智地使用数组。当使用普通变量就能简单地完成同样的事情时，不要无缘无故地制作数组.

在我看来，使用 x、y、z 实际上比使用数组 pos[3] 更易于阅读.

速度测试:

> 数组: 2444,2448,2473

> 普通变量: 972,975,963

非数组版本比数组版本快 2.5 倍.

速度测试代码:
```pawn
#include <open.mp>
 
main()
{
    for(new j = 0;j < 5;j++)
    {
        new a = GetTickCount();
        for(new i = 0;i < 10000000;i++)
        {
            new x,y,z;
            x = 12;
            y = 12342;
            z = 123123;
            x = 12;
            y = 12342;
            z = 123123;
            x = 12;
            y = 12342;
            z = 123123;
            x = 12;
            y = 12342;
            z = 123123;
            if(x == 0) {}
            else if(y == 0) {}
            else if(z == 0) {}
            X(x,y,z);
        }
        new b = GetTickCount();
        printf("速度测试 %d",b-a);
        
        a = GetTickCount();
        for(new i = 0;i < 10000000;i++)
        {
            new arr[3];
            arr[0] = 12;
            arr[1] = 12342;
            arr[2] = 123123;
            arr[0] = 12;
            arr[1] = 12342;
            arr[2] = 123123;
            arr[0] = 12;
            arr[1] = 12342;
            arr[2] = 123123;
            arr[0] = 12;
            arr[1] = 12342;
            arr[2] = 123123;
            if(arr[0] == 0) {}
            else if(arr[1] == 0) {}
            else if(arr[2] == 0) {}
            X(arr[0],arr[1],arr[2]);
        }
        b = GetTickCount();
        printf("速度测试 %d \n\n",b-a);
    }
}
stock X(x,y,z) {}
```

## 事先知道函数名时不要使用CallLocalFunction和funcidx

你知道 CallLocalFunction 和 funcidx 是速度很慢的函数吗？它们非常慢，因为它们需要在所有公共函数列表中检查作为参数传递的函数名称。这意味着大量的内部 strcmps.

```pawn
if(funcidx("OnPlayerEatBanana") == -1)
```
实际上，你并不需要这一行。你可以用 “0 ”指令来做同样的事情。如果您已经知道函数名称，那么只需使用预处理[指令](https://sampwiki.blast.hk/wiki/Keywords:Directives)来查找函数是否存在即可.

```pawn
#if defined OnPlayerEatBanana
//已声明 OnPlayerEatBanana
#endif
```
```pawn
if(CallLocalFunction("OnPlayerEatBanana","ii",playerid,bananaid"))
```
你可以
```pawn
#if defined OnPlayerEatBanana
if(OnPlayerEatBanana(playerid,bananaid))
#else
//该函数尚未声明
#endif
```
速度测试: (1个公共函数，0个参数)

> 直接调用: 204,226,218

> CallLocalFunction: 1112,1097,1001


请注意，这是 CallLocalFunction 的最佳情况。实际上，CallLocalFunction 会慢得多，因为会有很多公共函数.

## 本地代码比PAWN代码快得多
当有本地程序可以完成时，避免创建自己的函数（或者结合使用本地程序）.

本地程序之所以快得多，是因为本地函数是由计算机直接执行的，而所有 PAWN 代码都是在虚拟计算机中执行的。对于每一条 PAWN 指令，AMX 机器（虚拟计算机）都必须先解码指令，然后获取操作数，最后执行指令。解码和获取操作数需要一些 CPU.

```pawn
stock strcpy(dest[], src[], sz = sizeof(dest))
{
    dest[0] = 0;
    return strcat(dest, src, sz); //请注意，我使用了 strcat 而不是编写自己的循环
}
```

速度测试:

基于循环的 strcpy 与本地 strcat

下面是两个与 strcpy 等效的函数.

```pawn
stock strcpyY(dest[],const src[],sz=sizeof(dest))
{
    dest[0] = 0;
    strcat(dest,src);
}

stock strcpyX(dest[],const src[],sz=sizeof(dest))
{
    for(new i = 0; src[i];i++)
    {
        dest[i] = src[i];
    }
}
```

> 使用本地: 697,700,718,705

> 不使用本地: 5484,5422,5507,5562

## 循环中的条件

我不知道跟别人说过多少次了，但仍有少数人不做这个简单的优化.

代码1:
```pawn
for(new i = 0;i <= GetPlayerPoolSize();i++) {}
```
代码2:
```pawn
for(new i = 0,j = GetPlayerPoolSize();i <= j;i++) {}
```
在第一段代码中，每次迭代都会调用 GetPlayerPoolSize。在我们的时间框架中，每次调用 GetPlayerPoolSize 都会返回一个常量值。那么，为什么要在每次迭代时简单地调用 GetPlayerPoolSize 呢?

这正是第二段代码所避免的。它创建了一个局部变量，用于存储 GetPlayerPoolSize 返回的值，并在条件中使用它。因此只需调用一次函数，避免了函数开销.

速度测试:
> 进行优化: 1102,1080,1069,1091

> 不进行优化: 2374,2359,2429,2364

测试代码:
```pawn
new maxp = 100;

stock func()
{
    return maxp;
}

main()
{
    for(new j = 0;j < 5;j++)
    {
        new a = GetTickCount();
        for(new i = 0;i < 1000000;i++)
        {
            for(new i = 0,j = func(); i < j;i++) {}
        }
        new b = GetTickCount();
        printf("速度测试 %d",b-a);
        
        a = GetTickCount();
        for(new i = 0;i < 1000000;i++)
        {
            for(new i = 0; i < func();i++) {}
        }
        b = GetTickCount();
        printf("速度测试 %d \n\n",b-a);
    }
}
```

虽然在上述情况下，与您在循环内编写的代码相比，改进可能微不足道，但有时您会使用更慢的函数.

```pawn
for(new i = 0; i < CallRemoteFunction("GetPlayersInTeam", "i", TEAM_ID); i++) 
{
  
}
```
## 为多个变量赋值

代码1:
```pawn
x = abc;
y = abc;
z = abc;
```
代码2:
```pawn
x = 
y = 
z = abc;
```
您认为哪种代码更快?

代码1:
```pawn
load.pri c ;获取 abc
stor.pri 8 ;储存进 X 里
break	; 20
load.pri c ;获取 abc
stor.pri 4 ;储存进 Y 里
break	; 34
load.pri c ;获取 abc
stor.pri 0 ;储存进 Z 里
```
代码2:
```pawn
load.pri c ;获取 abc
stor.pri 0 ;储存进 X 里
stor.pri 4 ;储存进 Y 里
stor.pri 8 ;储存进 Z 里
```
看到区别了吗？第一个代码有额外的无用指令，在 abc 已经存在的情况下反复获取，而第二个版本只获取一次 abc 并设置 x、y、z.

显而易见的结论是代码 2 更快，但这可能微不足道.

当需要将大型数组设置为 0、1 或其他值时，使用 memset.

速度测试:

> 使用 memset 将 100 个元素的数组 (3D) 中的所有元素设置为零: 363,367,372

> 使用 for 循环将包含 100 个元素的数组 (3D) 中的元素设置为零: 6662,6642,6687

## 延迟声明局部变量

我见过把所有局部变量都放在函数顶端的脚本，尽管有些局部变量有时是需要的.

不好的代码
```pawn
public OnPlayerDoSomething(playerid)
{
    new actionid = GetPlayerAction(playerid), pee_id, peed_on_whome, amount_of_pee;
    if(actionid == PLAYER_PEE)
    {

    }
}
```
良好的代码:
```pawn
public OnPlayerDoSomething(playerid)
{
    new actionid = GetPlayerAction(playerid);
    if(actionid == PLAYER_PEE)
    {
        new pee_id,peed_on_whome,amount_of_pee;
    }
}
```
如果你看过我前面的提示，现在一定已经知道，当你创建一个局部变量时，编译器首先会在堆栈中为它创建一些空间，然后将其初始化为零.

因此，如果不确定是否会使用局部变量，就不能简单地创建局部变量。第二段代码只有在需要时才会创建局部变量，而在第一段代码中，即使不使用也会创建局部变量.

这对少数变量的性能影响不大，但却提高了代码的可读性.

## 简化和重写数学运算以避免昂贵的操作

下面是一个经典示例，可以提高该片段的性能:
```pawn
new Float:x,Float:y,Float:z;
GetPlayerVelocity(playerid,x,y,z);
if(floatsqrt( (x*x) + (y*y) + (z*z)) > 5.0)
```
```pawn
new Float:x,Float:y,Float:z;
GetPlayerVelocity(playerid,x,y,z);
if( ((x*x) + (y*y) + (z*z)) > 25.0)
```
您是否注意到以下变化?

我对 if 语句中的条件两边进行了平方处理，并删除了慢速函数 “floatsqrt”。

这里是另一个:
```pawn
for(new i = 0, j = GetTickCount(); i < 10; i++)
{
    if( j - LastTick[i] > MAX_TIME_ALLOWED)
    {
    
    }
}
```
```pawn
for(new i = 0, j = GetTickCount() - MAX_TIME_ALLOWED; i < 10; i++)
{
    if(j > LastTick[i])
    {

    }
}
```
我去掉了条件中的 MAX_TIME_ALLOWED。现在减法只做一次，而在第一段代码中每次都要做。即使是这样的改进也是微不足道的，除非你有消耗大量 CPU 的操作.

## memcpy和strfind等操作也适用于数组

毕竟，字符串和数组是一样的。唯一不同的是，字符串以空字符结束，而普通数组不以空字符结束.

```pawn
new DefaultPlayerArray[100] = {1,2,3,4,5,6,7,8,9,10};
new PlayerArray[MAX_PLAYERS][100];

for(new i = sizeof(DefaultPlayerArray); i != -1; i--)
{
    PlayerArray[playerid][i] = DefaultPlayerArray[i];
}
```
下面是另一个等效代码
```pawn
memcpy(PlayerArray[playerid], DefaultPlayerArray, 0, sizeof(DefaultPlayerArray)*4, sizeof(PlayerArray[]));
```
我对这两个代码进行了一些基准测试，结果如下:

循环版本:
- 4286ms
- 4309ms
- 4410ms

memcpy版本:
- 60ms
- 62ms
- 60ms

同样，您也可以在数组上使用 strfind、strmid 和许多其他字符串函数。唯一的问题是，当 str 函数在数组中找到元素 “0 ”时，函数会终止，因为值 0 意味着“\0”，即：空字符.

## 使用CallRemoteFunction真的值得吗

首先，我想说 CallRemoteFunction 的速度慢得可怕，必须尽可能避免使用。CallRemoteFunction 通常用于在其他脚本中设置反作弊时更新玩家变量.

你有没有想过在每个脚本中都设置反作弊功能？实际上，我的游戏模式中就有一个反作弊脚本，它可以确保数据库中不更新修改过的数据，而管理过滤脚本中的另一个反作弊脚本则负责对作弊行为采取行动（独立工作）.

为什么有两个反作弊？我们有两个选择，要么制作两个反作弊程序，要么使用 CallRemoteFunction 更新玩家变量.

事实上，某些反作弊检查所需的时间只有 CallRemoteFunction 调用更新函数所需的时间的四分之一.

在每个脚本中计算一些玩家变量并不重要。这比在一个脚本中更新并使用 CallRemoteFunction 访问要好得多.

## 多次访问数组元素

让我们举个例子来理解我们在这里谈论的内容
```pawn
new val = value[x][y][z];
for(new i = 50; i != -1; --i) Arr[i] = val;
```
```pawn
for(new i = 50; i != -1; --i) Arr[i] = value[x][y][z];
```
您认为哪个更快?

第一种方法会更快。要知道，根据数组索引计算正确的地址需要一些时间。在第二段代码中，每次将值复制到 Arr 时都要计算地址，而在第一段代码中，我们只计算一次地址.

如果要多次访问一个数组元素，请在本地变量中创建一个数组元素的临时副本，然后使用本地变量.

速度测试:
> 代码1:2280,2330,2350

> 代码2:8008,8183,8147

## 不要在表达式中混合使用浮点数和整数

by Mauzen

也许这一条太简单了，但我至少想补充一下，因为我经常看到有人犯这样的 "错误”.

切勿混用浮点数和整数（即使不会发出标记不匹配警告）。始终在一条语句中使用相同的数据类型.

比如
```pawn
new Float:result = 2.0 + 1;

// 被编译为
new Float:result = 2.0 + float(1);

// 明显慢于
new Float:result = 2.0 + 1.0;
```

```pawn
new mindist = 10;
if (GetPlayerDistanceFromPoint(playerid, 237.9, 115.6, 1010.2) < mindist)

// 被编译为
new mindist = 10;
if (GetPlayerDistanceFromPoint(playerid, 237.9, 115.6, 1010.2) < float(mindist))

// 明显慢于
new Float:mindist = 10.0;
if (GetPlayerDistanceFromPoint(playerid, 237.9, 115.6, 1010.2) < mindist)
```
对于复杂的数学任务，一个简单的 0 可以使速度相差几个百分点.

## 不必要地使用Streamer

即使只需要 10 或 20 个地图图标、50 个对象等，使用Streamer已成为每个人的习惯.

你知道什么是Streamer吗？Streamer 是一个插件/包含程序，它允许你绕过 SAMP 的限制。SAMP 最多允许 1000 个对象，而你不能拥有比这更多的对象.

Streamer 可以绕过这一限制，当有玩家在对象的绘制距离内时创建对象，当对象附近没有玩家时销毁对象。因此，Streamer基本上是在需要时创建对象，在不需要时销毁对象。通过这种方式，它可以跨越 SAMP 限制.

使用 Streamer 函数（例如 CreateDynamicObject）时，streamer 并不真正创建对象。它会将对象信息（X,Y,Z,RotX,RotY,RotZ....）添加到对象数据库中。在一定数量的服务器时间/周期过去后，它会查看数据库中的所有对象，检查是否有玩家接近某个对象，并在需要时创建该对象.

相关片段:
- Streamer将对象的信息添加到数据库中  [详情点击此处](https://github.com/samp-incognito/samp-streamer-plugin/blob/master/src/natives/objects.cpp#L36)
- 玩家开始更新  [详情点击此处](https://github.com/samp-incognito/samp-streamer-plugin/blob/master/src/natives/objects.cpp#L36)
- 负责更新对象的函数  [详情点击此处](https://github.com/samp-incognito/samp-streamer-plugin/blob/master/src/streamer.cpp#L456)

当对象少于 1000 个时，使用Streamer是否有意义?
你真的需要Streamer吗?

不需要!

如果您确定不会超过 SAMP 限制，那么您就不需要使用Streamer.

这就带来了一个新问题，假设在现有版本中，您有 500 个对象，但您要更新的脚本需要 1500 个对象。那么现在您是否需要将所有 SAMP 对象原生转换为Streamer原生呢?

如果你的初始版本写得很聪明，就不会.

我的做法是:
```pawn
#define CreateDynamicObject CreateObject
```
现在，即使没有Streamer，您也可以在代码中使用 CreateDynamicObject.

当您知道需要使用Streamer时，只需移除定义并加入Streamer.

一个更巧妙的方法是使用 CreateObject 来创建热门区域的对象，热门区域是指经常有玩家出现的区域，例如可以假定玩家几乎总是会出现在出生点的对象。而对于那些位于偏远位置且玩家几乎不会访问的对象，您一定要使用 CreateDynamicObject，因为这些对象不需要一直存在，而热门对象则一定会一直存在/或者频繁创建（即使与Streamer一起使用也是如此，因此对此类对象使用Streamer并不划算）.

由于 streamer 是一个插件，因此与 PAWN 代码相比，它的速度相当快，因此您必须对许多对象进行处理，才能看到合理的改进.

同样，您也可以为其他本地函数这样做.

## 函数使用的好坏-优化二维数组操作代码

很多人都相信一个常见的神话，那就是函数调用非常昂贵，但事实并非如此。事实上，原始函数调用（空）比引用二维数组快很多倍.

```pawn
native SLE_algo_foreach_list_init(list:listid, &val);
native SLE_algo_foreach_list_get(feid);

#define foreach::list(%0(%1)) for(new %1, fel_%0@%1_id = SLE_algo_foreach_list_init(%0, %1); SLE_algo_foreach_list_get(fel_%0@%1_id);)
```
如果你仔细观察，它只是调用一个函数来获取值，这比取消引用数组要快。为了彻底清除迷雾，该函数是在插件中定义的，否则显然不会更快，因为确实使用了一个数组，但那是在插件内部

这个例子只是为了说明，与你编写的其他代码相比，函数调用的成本并不高。这意味着，在必要时，您应该为大段代码创建函数，以完成特定任务，尤其是如果这样做能提高代码的可读性的话.

然而，滥用函数可能会付出高昂的代价，特别是在前面讨论过的循环中.

以下是使用一维/二维阵列效果更好的一种情况.

引用Vince的一段话:
我经常看到的一件事是，GetPlayerName 的使用率过高，这在第一篇帖子中没有提到。玩家在连接时无法更改自己的名字（当然，SetPlayerName 是个例外），因此反复调用该函数似乎是多余的。我认为使用包装器更糟糕。只需在玩家连接时将其存储到一个变量中，然后在任何地方都使用该变量即可。GetPlayerIP 也是如此。
还有一种说法是 “制作函数总是比较慢”，但在处理多维数组时，这种说法并不正确。如果方法得当，创建函数实际上可以显著提高性能

```pawn
for(new y = 0; y < 100; y++)
{
    Array[playerid][y] = y;
}
```
上面的代码比以下代码要慢得多

```pawn
stock DoSomething(arr[])
{
    for(new y = 0; y < 100; y++)
    {
        arr[y] = y;
    }
}
```
原因就在于汇编代码的根源。只要看一看 PAWN 中数组是如何被取消引用的，就会明白这一点.

这是引用二维数组所涉及的代码量
```pawn
#emit CONST.alt arr //加载数组地址
#emit CONST.pri 2 //我们要访问第二个子数组
#emit IDXADDR //主数组第二个元素的地址

#emit MOVE.alt //保留该地址的副本，因为我们需要将其添加到偏移量中，以获取子数组的地址

//ALT = PRI = 主数组第二个元素的地址
#emit LOAD.I

//ALT = 主数组第二个元素的地址
//PRI = 相对于 ALT 中存储的地址的偏移量，指向第 2 个子数组
#emit ADD

//PRI 现在有了子数组的地址
#emit MOVE.alt //将子数组第一个元素的地址从 PRI 移至 ALT

#emit CONST.pri 4 //我们需要子数组的第 4 个元素
#emit LIDX//加载存储在 arr[2][4] 中的值
```
将其与对一维数组的取消引用进行比较
```pawn
#emit CONST.alt array_address
#emit CONST.pri n
#emit IDXADDR //PRI 现在有了第 (n + 1) 个元素的地址

#emit CONST.alt array_address
#emit CONST.pri n
#emit LIDX //PRI 现在的值存储在第 (n + 1) 个元素中
```
数组是这样传递的
```pawn
 //推送全局字符串地址
  #emit PUSH.C global_str 

  //推送本地字符串
  #emit PUSH.S cmdtext
```
这就清楚地解释了推数组的工作原理。当你推数组时，你推的是数组的地址，所以在函数调用中，你接收到的是一维数组。在这种情况下，每次迭代都会跳过二维数组取消引用代码的前半部分，因此速度会快很多.

遗憾的是，PAWN 没有提供指针.
